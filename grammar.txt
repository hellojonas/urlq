query               ::= logic_or ;

logic_or            ::= login_and ( "[or]" login_and ) * ;

login_and           ::= expression ( "[and]" expression )* ;

expression          ::= equality 
                        | difference
                        | not_contain
                        | contain
                        | lesser
                        | lesser_equal
                        | greater
                        | greater_equal
                        | include
                        | not_include
                        | between 
                        | group ;


group               ::= "(" query ")" ;

equality            ::= path "[:]" primary ;
difference          ::= path "[~:]" primary ;
contain             ::= path "[#]" primary ;
not_contain         ::= path "[~#]" primary ;
lesser              ::= path "[lt]" primary ;
lesser_equal        ::= path "[lte]" primary ;
greater             ::= path "[gt]" primary ;
greater_equal       ::= path "[gte]" primary ;
include             ::= path "[in]" array ;
not_include         ::= path "[~in]" array ;
between             ::= path "[::]" array ;

path                ::= IDENTIFIER ( "_" IDENTIFIER )* ;

array               ::= primary ( "," primary )* ;

primary             ::=  STRING
                        | NUMBER
                        | IDENTIFIER
                        | TRUE
                        | FALSE 
                        | "$\"" DATE "\"" ;

